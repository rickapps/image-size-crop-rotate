; Script generated by the HM NIS Edit Script Wizard.
; AMAFS GOLD
!define TEMP1 $R0 ;Temp variable
; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "All My Auctions"
!define PRODUCT_VERSION "Gold"
!define PRODUCT_PUBLISHER "RAJE Software"
!define PRODUCT_WEB_SITE "http://www.amafs.com/gold"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\AucSell.exe"
!define PRODUCT_REGKEY "Software\RAJE Software\All My Auctions"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "DumpLog.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "LicenseTM.rtf"
; Serial Number Entry
Page custom SetSNEntry ValidateSN ": Entry Serial Number"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\AucSell.exe"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\Readme.txt"
!define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS
ReserveFile "SNPage.ini"
; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup.exe"
InstallDir "$PROGRAMFILES\AucSeller"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show
BrandingText "RAJE Software   Questions or problems email support@amafs.com"

Function .onInit
  ClearErrors
  ; IE 5 or above is required
  Call GetIEVersion
  Pop $0
  IntCmp $0 5 +3 0 +3
  MessageBox MB_ICONSTOP 'You must have Internet Explorer 5 or higher installed. You have version $0. Download a newer version of IE from www.microsoft.com'
  Abort

  ;Make sure we have the authority to run
  UserInfo::GetName
  IfErrors OkToInstall
  Pop $1
  UserInfo::GetAccountType
  Pop $0
  StrCmp $0 "Admin" OkToInstall
  StrCmp $0 "Power" OkToInstall
  MessageBox MB_ICONSTOP 'User "$1" does not have install privileges. Log in as a different user and try again.'
  Abort
OkToInstall:
  File /oname=$PLUGINSDIR\SNPage.ini "SNPage.ini"
FunctionEnd

Function .onInstFailed
  SetOutPath "$TEMP"
  Push "AMAFS.htm"
  Call DumpLog
  ExecShell 'open' '$TEMP\AMAFS.htm'
FunctionEnd

Function SetSNEntry

  ;Display the custom serial number page

  Push ${TEMP1}

    InstallOptions::dialog "$PLUGINSDIR\SNPage.ini"
    Pop ${TEMP1}

  Pop ${TEMP1}

FunctionEnd

Function ValidateSN
  ReadINIStr ${TEMP1} "$PLUGINSDIR\SNPage.ini" "Field 1" "State"
  StrCmp ${TEMP1} "JOEVENPHRGU" Done ChkOut
  ChkOut:
    Abort "Invalid Serial Number"

  Done:

FunctionEnd

Function Droppings
   ;Get the current time
   GetTempFileName $R8
   GetFileTime $R8 $R6 $R7
   Delete $R8
   ;Store to the application folder - assume SetShellVarContext has already been set
   CreateDirectory '$APPDATA\AucSeller'
   FileOpen $0 '$APPDATA\AucSeller\License.reg' 'w'
   IfErrors Failed
   FileWrite $0 $R6
   IfErrors Failed
   FileWriteByte $0 "9"
   FileWrite $0 $R7
   IfErrors Failed
   FileClose $0
   IfErrors Failed
   ; Adjust the security on the license file so USERs can access it
   Call GetWindowsVersion
   Pop $R1
   StrCmp $R1 '2000' Adjust
   StrCmp $R1 '2003' Adjust
   StrCmp $R1 'XP' Adjust Inventory
   Adjust:
   AccessControl::GrantOnFile '$APPDATA\AucSeller\License.reg' '(S-1-5-32-545)' 'GenericWrite'

   ;Take inventory
   Inventory:
   IntOp $1 0 + 0  ;$1 is a status. Initialize to zero
   FileOpen $0 '$WINDIR\Win.ini' r
   IfErrors +3
   ; Update value of $1 with ini file times
   System::Call 'kernel32::GetFileTime(i, *l, *l, *l) i (r0, .r1, .r2, .r3)'
   FileClose $0

   ReadINIStr $R4 "Win.ini" "StdCalc" "Mem1"
   IfErrors +3
   ReadINIStr $R5 "Win.ini" "StdCalc" "Mem2"
   IfErrors 0 Drop
   ; Use current time
   StrCpy $R4 $R6
   StrCpy $R5 $r7

   Drop:
   WriteINIStr "Win.ini" "StdCalc" "Mem1" $R4
   IfErrors Failed
   WriteINIStr "Win.ini" "StdCalc" "Mem2" $R5
   IfErrors Failed

   ;Cover our tracks. Reset file times to original values
   IntCmp $1 0 +4  ;If we could not get original times, skip this part
   FileOpen $0 '$WINDIR\Win.ini' a
   ; Do not use r2 here - when you open the file, r2 is set to the current time
   ; Use r3 - that way the last access time is set to the last write time
   System::Call 'kernel32::SetFileTime(i, *l, *l, *l) i (r0, r1, r3, r3)'
   FileClose $0

   ;Convert our values to hex
   IntFmt $R6 "%X" $R4
   IntFmt $R7 "%X" $R5
   IntOp $R8 $R4 & $R5
   WriteRegStr HKLM "${PRODUCT_REGKEY}\AucSeller\Window" "PosX" "$R6"
   IfErrors Failed
   WriteRegStr HKLM "${PRODUCT_REGKEY}\AucSeller\Window" "PosY" "$R7"
   IfErrors Failed
   WriteRegDWORD HKLM "${PRODUCT_REGKEY}\AucSeller\License" "Salt" $R8
   IfErrors Failed
   GoTo exit

   Failed:
       Abort 'Could not initialize marks'
   exit:
FunctionEnd

Section "MainSection" SEC01
  Call GetWindowsVersion
  Pop $R1
  DetailPrint 'Install AMAFS-TM started on Windows $R1'
  Call GetIEVersion
  Pop $R1
  DetailPrint 'Running Internet Explorer $R1'
  DetailPrint 'Installing ${PRODUCT_NAME} ${PRODUCT_VERSION}'
  SetShellVarContext all
  Call Droppings
  SetOutPath "$DOCUMENTS"
  SetOverwrite on
  File "Files\German.cta"
  File "Files\Demo.pro"
  File "Files\AucField.fee"
  File "Files\AucClip.cta"
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File "Files\AucUtils.dll"
  File "Files\AucSite.dll"
  File "Files\AucSell.exe"
  File "Files\AucImprt.dll"
  File "Files\Template.exe"
  File "Readme.txt"
  CreateDirectory "$SMPROGRAMS\All My Auctions GOLD"
  CreateShortCut "$SMPROGRAMS\All My Auctions GOLD\All My Auctions.lnk" "$INSTDIR\AucSell.exe"
  CreateShortCut "$DESKTOP\All My Auctions.lnk" "$INSTDIR\AucSell.exe"
  File "Files\AucSell.chm"
  CreateShortCut "$SMPROGRAMS\All My Auctions GOLD\Help.lnk" "$INSTDIR\AucSell.chm"
  RegDLL $INSTDIR\AucSite.dll
  RegDLL $INSTDIR\AucImprt.dll
SectionEnd

Section -RegSetup
  ; Database file
  WriteRegStr HKCU "${PRODUCT_REGKEY}\AucSeller\Settings" "ProfileName" "$DOCUMENTS\Demo.pro"
  ; Double Space
  WriteRegDWORD HKCU "${PRODUCT_REGKEY}\AucSeller\Settings" "Spacing" 1
  ; Export File
  WriteRegStr HKCU "${PRODUCT_REGKEY}\AucSeller\Export" "Columns" "MABCDEFGHIJKL"
  WriteRegStr HKCU "${PRODUCT_REGKEY}\AucSeller\Export" "Target" "$INSTDIR\Export.csv"
  WriteRegDWORD HKCU "${PRODUCT_REGKEY}\AucSeller\Export" "Type" 0
  ; Websites
  WriteRegStr HKCU "${PRODUCT_REGKEY}\AucSeller\Websites" "String0" "http://www.amafs.com/gold"

  ; Combo Boxes
  WriteRegStr HKLM "${PRODUCT_REGKEY}\AucSeller\Combos" "Payment Method" "Cash;Check;Money Order;PayPal Std;PayPal Std+;PayPal-Int;"
  WriteRegStr HKLM "${PRODUCT_REGKEY}\AucSeller\Combos" "Shipping Method" "First Class;Priority;Book Rate;"
  WriteRegStr HKLM "${PRODUCT_REGKEY}\AucSeller\Combos" "Weight" "Ounce;Under 1 lb;Over 1 lb;"
  WriteRegStr HKLM "${PRODUCT_REGKEY}\AucSeller\Combos" "Feedback" "None;Positive;Neutral;Negative;Waiting for Buyer;"
  WriteRegStr HKLM "${PRODUCT_REGKEY}\AucSeller\Combos" "Auction Type" "Auction;Fixed Price;Store;AutoRenew;"
  ;Eliminate license key
  WriteRegStr HKLM "${PRODUCT_REGKEY}\AucSeller\License" "Code" "JOEVENPHRGU"

SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\All My Auctions GOLD\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\All My Auctions GOLD\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\AucSell.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\AucSell.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\Readme.txt"
  Delete "$INSTDIR\Template.exe"
  Delete "$INSTDIR\AucImprt.dll"
  Delete "$INSTDIR\AucSell.chm"
  Delete "$INSTDIR\AucSell.exe"
  Delete "$INSTDIR\AucSite.dll"
  Delete "$INSTDIR\AucUtils.dll"
  SetShellVarContext all
  Delete "$DOCUMENTS\Demo.pro"
  Delete "$DOCUMENTS\AucClip.cta"
  Delete "$DOCUMENTS\AucField.fee"
  Delete "$DOCUMENTS\German.cta"

  Delete "$SMPROGRAMS\All My Auctions GOLD\Uninstall.lnk"
  Delete "$SMPROGRAMS\All My Auctions GOLD\Website.lnk"
  Delete "$SMPROGRAMS\All My Auctions GOLD\Help.lnk"
  Delete "$DESKTOP\All My Auctions.lnk"
  Delete "$SMPROGRAMS\All My Auctions GOLD\All My Auctions.lnk"

  RMDir "$SMPROGRAMS\All My Auctions GOLD"
  RMDir "$INSTDIR"

  DeleteRegKey HKLM "${PRODUCT_REGKEY}"
  DeleteRegKey /ifempty HKLM "Software/RAJE Software"
  DeleteRegKey HKCU "${PRODUCT_REGKEY}"
  DeleteRegKey /ifempty HKCU "Software/RAJE Software"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd
